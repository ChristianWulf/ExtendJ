apply plugin: 'java'
apply plugin: 'application'
apply plugin: JastAddPlugin

JastAddPlugin.modules(["java4", "java5", "java6", "java7"])

jastaddj {
	// JastAddJ build parameters
	module = "java7 backend"
	javaVersion = "Java SE 7"

	astPackage = "AST"
	sourceDir = "src"
	toolsDir = "tools"
	resourceDir = "${sourceDir}/res"

	binDir = "ant-bin"
	tmpDir = "${sourceDir}/tmp"
	genDir = "${sourceDir}/gen"
	genResDir = "${sourceDir}/gen-res"
}

mainClassName = 'org.jastadd.jastaddj.JavaCompiler'
jar.manifest.attributes 'Main-Class': "${mainClassName}"

// generate jastaddj.jar in this directory:
jar.destinationDir = new File(".")

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

sourceSets {
	main {
		java {
			srcDir "${project.jastaddj.sourceDir}/frontend"
			srcDir "${project.jastaddj.sourceDir}/backend"
			srcDir project.jastaddj.genDir
		}
		resources {
			srcDir "${project.jastaddj.genResDir}"
			srcDir "${project.jastaddj.resourceDir}"
		}
	}
}

dependencies {
	compile files("${project.jastaddj.toolsDir}/ant-1.9.1.jar")
}

task javaFiles << {
	// this task is for debugging the gradle build
	println "files in java component:"
	JastAddModule.get(project.jastaddj.module).files(project, "java").each{ println it }
}

